NeosRulez.NodeSearch.Module.SearchTermController.index = NeosRulez.NodeSearch:Module.SearchTermController.Index

prototype(NeosRulez.NodeSearch:Module.SearchTermController.Index) < prototype(Neos.Fusion:Component) {

    renderer = afx`
        <div class="neos-row-fluid neos-module-container">
            <p @if.render={!items || Array.length(items) == 0}>
                {I18n.translate('NeosRulez.NodeSearch:Main:noItems')}
            </p>
            <table @if.render={items && Array.length(items) > 0} class="neos-table">
                <thead>
                <tr>
                    <td>{I18n.translate('NeosRulez.NodeSearch:Main:searchTerm')}</td>
                    <td>{I18n.translate('NeosRulez.NodeSearch:Main:searchResults')}</td>
                    <td>{I18n.translate('NeosRulez.NodeSearch:Main:dateTime')}</td>
                </tr>
                </thead>
                <tbody>
                    <Neos.Fusion:Loop items={items}>
                        <tr>
                            <td>{item.searchTerm}</td>
                            <td>{item.searchCount}</td>
                            <td>{Date.format(item.created, 'Y-m-d H:i')}</td>
                        </tr>
                    </Neos.Fusion:Loop>
                </tbody>
            </table>
            <NeosRulez.NodeSearch:Component.Pagination />

            <div class="neos-footer">
                <Neos.Fusion.Form:Form form.target.action="deleteAll" form.target.controller="Module\\SearchTerm" >
                    <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-danger" attributes.onclick={'return confirm("' + I18n.translate('NeosRulez.NodeSearch:Main:deleteConfirmation') + '");'} >
                        <i class="fas fa-trash"></i> {I18n.translate('NeosRulez.NodeSearch:Main:deleteAll')}
                    </Neos.Fusion.Form:Button>
                </Neos.Fusion.Form:Form>
            </div>
        </div>
    `
}

prototype(NeosRulez.NodeSearch:Component.Pagination) < prototype(Neos.Fusion:Component) {

    alignTop = false

    renderer = afx`
        <div @if.render={pagination} class="page-navigation">
            <ul class="neos-fluid-widget-paginator" style={props.alignTop ? 'border-top:0;border-bottom:1px solid #3f3f3f' : false} >
                <li @if.render={page > 1 ? true : false} class="previous" style={props.alignTop ? 'border-bottom:0;border-top:1px solid #3f3f3f' : false} >
                    <Neos.Fusion:Tag tagName="a" @children="attributes.href" content="vorherige" >
                        <Neos.Fusion:UriBuilder package="NeosRulez.NodeSearch" controller="Module\\SearchTerm" action="index" @children="arguments">
                            <Neos.Fusion:DataStructure page={(page - 1)} offset="0" />
                        </Neos.Fusion:UriBuilder>
                    </Neos.Fusion:Tag>
                </li>
                <Neos.Fusion:Loop items={pagination} itemName="pageItem">
                    <NeosRulez.NodeSearch:Component.Pagination.Item page={page} pageItem={pageItem} action={props.action} recipientList={recipientList} alignTop={props.alignTop} />
                </Neos.Fusion:Loop>
                <li @if.render={page >= pages ? false : true} class="next" style={props.alignTop ? 'border-bottom:0;border-top:1px solid #3f3f3f' : false} >
                    <Neos.Fusion:Tag tagName="a" @children="attributes.href" content="nÃ¤chste" >
                        <Neos.Fusion:UriBuilder package="NeosRulez.NodeSearch" controller="Module\\SearchTerm" action="index" @children="arguments">
                            <Neos.Fusion:DataStructure page={(page + 1)} offset="0" />
                        </Neos.Fusion:UriBuilder>
                    </Neos.Fusion:Tag>
                </li>
            </ul>
        </div>
    `
}

prototype(NeosRulez.NodeSearch:Component.Pagination.Item) < prototype(Neos.Fusion:Component) {
    isPage = ${this.page == this.pageItem ? true : false}
    className = ${this.isPage ? 'current' : false}

    alignTop = false

    renderer = afx`
        <li class={props.className} style={props.alignTop ? ('border-bottom:0;' + (props.className == 'current' ? 'border-top:1px solid #3f3f3f' : '')) : false} >
            <Neos.Fusion:Tag @if.render={!props.isPage} tagName="a" @children="attributes.href" content={pageItem} >
                <Neos.Fusion:UriBuilder package="NeosRulez.NodeSearch" controller="Module\\SearchTerm" action="index" @children="arguments">
                    <Neos.Fusion:DataStructure page={pageItem} offset="0" />
                </Neos.Fusion:UriBuilder>
            </Neos.Fusion:Tag>
            {props.isPage ? pageItem : false}
        </li>
    `
}
